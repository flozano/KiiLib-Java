apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withSourcesJar()
    withJavadocJar()
}

dependencies {
    api 'org.json:json:20240303'
    testImplementation 'junit:junit:4.13.2'
}

publishing {
    repositories {
        maven {
            def releaseRepo = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotRepo = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            name = "OSSRH"
            url = !project.version.toString().endsWith("-SNAPSHOT") ? releaseRepo : snapshotRepo
            credentials {
                username = findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                password = findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
            }
        }
    }
    publications {
        maven(MavenPublication) { publication ->
            from components.java
            pom {
                groupId = project.group
                artifactId = project.name
                version = project.version
                name = project.name
                description = Meta.desc
                packaging = 'jar'
                url = "https://github.com/${Meta.githubRepo}"
                licenses {
                    license {
                        name = Meta.license
                        url = Meta.licenseUrl
                    }
                }
                developers {
                    developer {
                        id = Meta.developerId
                        name = Meta.developerName
                        email = Meta.developerEmail
                    }
                }
                scm {
                    url = "https://github.com/${Meta.githubRepo}.git"
                    connection = "scm:git:git://github.com/${Meta.githubRepo}.git"
                    developerConnection = "scm:git:git://github.com/${Meta.githubRepo}.git"
                }
                issueManagement {
                    url = "https://github.com/${Meta.githubRepo}/issues"
                }
            }
        }
    }
}


signing {
    def envSigningKey = System.getenv("GPG_SIGNING_KEY")
    def envSigningPassphrase = System.getenv("GPG_SIGNING_PASSPHRASE")
    if (envSigningKey && envSigningPassphrase) {
        useInMemoryPgpKeys(envSigningKey, envSigningPassphrase)
    }
    sign publishing.publications.maven
}

